#include "common.h"
#include "iostructs\p_volume_sun.h"

#undef ULTRA_SHADOWS_ON
#undef USE_ULTRA_SHADOWS

#define RAY_PATH	2.0
#define JITTER_TEXTURE_SIZE	64.0

#define	JITTER_SUN_SHAFTS

#ifdef	SUN_SHAFTS_QUALITY
	#if SUN_SHAFTS_QUALITY==1
		#define	FILTER_LOW
		#define RAY_SAMPLES	20
	#elif SUN_SHAFTS_QUALITY==2
		#define	FILTER_LOW
		#define RAY_SAMPLES	20
	#elif SUN_SHAFTS_QUALITY==3
		#define	FILTER_LOW	
		#define RAY_SAMPLES	40
	#endif
#endif

#ifndef	FILTER_LOW
#ifdef USE_MINMAX_SM
#define SM_MINMAX
#endif
#endif

#include "shadow.h"

uniform float4 volume_range;	//	x - near plane, y - far plane
uniform float4 sun_shafts_intensity;
uniform float4 screen_res;

#ifdef MSAA_OPTIMIZATION
//#ifdef GBUFFER_OPTIMIZATION
//float4 	_main (float2 tc, float2 tcJ, float4 pos2d, uint iSample )
//#else
float4 	_main (v2p_volume  I, uint iSample  )
//#endif
#else
//#ifdef GBUFFER_OPTIMIZATION
//float4 	_main (float2 tc, float2 tcJ, float4 pos2d )
//#else
float4 	_main (v2p_volume  I)
//#endif
#endif
{
	float2	tc  = I.tc.xy/I.tc.w;
	float4	pos2d = I.hpos;
	
	gbuffer_data gbd = gbuffer_load_data( GLD_P(tc, pos2d, ISAMPLE) );

#ifndef	SUN_SHAFTS_QUALITY
	return float4(0,0,0,0);
#else	//	SUN_SHAFTS_QUALITY

	//float3	P = tex2D(s_position, tc).xyz;
	float3	P = gbd.P;

#ifndef	JITTER_SUN_SHAFTS
//	Fixed ray length, fixed step dencity
//	float3	direction = (RAY_PATH/RAY_SAMPLES)*normalize(P);	
//	Variable ray length, variable step dencity
	float3	direction = P/RAY_SAMPLES;
#else	//	JITTER_SUN_SHAFTS
//	float2	tcJ = I.tcJ;
//	Variable ray length, variable step dencity, use jittering
	//float4	J0 	= tex2D		(jitter0,tcJ);
	float4	J0 	= tex2D	( jitter0, tc*screen_res.x*1.0/JITTER_TEXTURE_SIZE );
	float	coeff = (RAY_SAMPLES - 1.0*J0.x)/(RAY_SAMPLES*RAY_SAMPLES);		
	float3	direction = P*coeff;
//	float3	direction = P/(RAY_SAMPLES+(J0.x*4-2));
#endif	//	JITTER_SUN_SHAFTS

	float	depth = P.z;
	float	deltaDepth = direction.z;
	
	float4	current = mul (m_shadow, float4(P, 1.0));
	float4	delta = mul (m_shadow, float4(direction, 0.0));

	float res = 0.0;
	float max_density = sun_shafts_intensity.x;
	float density = max_density/float(RAY_SAMPLES);

 	if (depth < 0.01) res = max_density; // filter for the sky

	for ( int i=0; i < RAY_SAMPLES; ++i )
	{
		if (depth > 0.3)
		{
#ifndef	FILTER_LOW
#ifndef SM_MINMAX
				res += density*shadow( current );
#else
				res += density*shadow_dx10_1_sunshafts(current, pos2d.xy );
#endif
#else	//	FILTER_LOW
				res += density*sample_hw_pcf(current, float4(0,0,0,0));
#endif	//	FILTER_LOW
		}

		depth -= deltaDepth;
		current -= delta;
	}

	float fSaturation = -Ldynamic_dir.z;

	//	Normalize dot product to
	fSaturation	= 0.5*fSaturation+0.5;
	//	Map saturation to 0.2..1
	fSaturation	= 0.80*fSaturation+0.20;
	res		*= fSaturation;

	return res*Ldynamic_color;
#endif	//	SUN_SHAFTS_QUALITY
}
